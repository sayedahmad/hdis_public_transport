group 'de.tu_berlin.dima.niteout'
version '0.1'

apply plugin: 'java'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.glassfish:javax.json:1.0.4'
    compile 'com.squareup.okhttp3:okhttp:3.5.0'
    compile 'commons-validator:commons-validator:1.5.1'
    //compile 'org.javatuples:javatuples:1.2'
    testCompile group: 'junit', name: 'junit', version: '4.11'
}

task deliverableJar(type: Jar) {
    baseName = project.name + '-inclibs'
    from { configurations.compile.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

test.doFirst {
    // load all keys
    def Properties props = new Properties()
    props.load(new FileInputStream(new File('api-keys.properties')))
    props.propertyNames().each { keyname ->
        systemProperty keyname, props.getProperty(keyname)
    }
}


